Program = Stmt*
Stmt = Return | WhileLoop | IfElse | IfThen | FunctionDef | FunctionCall | Assignment | Expr

Return = "return" value:Expr

WhileLoop = "while" condition:Expr "do" body:Block "end"

IfElse = "if" condition:Expr "then" then:Block "else" else:Block "endif"
IfThen = "if" condition:Expr "then" then:Block "endif"


FunctionDef = "function" name:Identifier "(" params:ParamList ")" "do" body:Block "end"
FunctionDef = "function" name:Identifier "(" ")" "do" body:Block "end"

Block = Stmt*

FunctionCall = name:Identifier "(" args:ArgList ")"
FunctionCall = name:Identifier "(" ")"

Assignment = name:Identifier "=" value:Expr

ParamList = Identifier "," params:ParamList
ParamList = Identifier

ArgList = Expr "," args:ArgList
ArgList = Expr

Expr = LogicalOr
LogicalOr = LogicalAnd "or" LogicalOr | LogicalAnd
LogicalAnd = Comparison "and" LogicalAnd | Comparison
Comparison = Term Eq Term | Term Neq Term | Term Lt Term | Term Gt Term | Term
Term = Factor Add Term | Factor Sub Term | Factor
Factor = Unary Mul Factor | Unary Div Factor | Unary Mod Factor | Unary
Unary = UnaryOp Unary | Atom

Atom = Float | Int | String | FunctionCall | Identifier | ListLiteral | "(" Expr ")"

ListLiteral = "[" Elements "]"
ListLiteral = "[" "]"

Elements = Expr "," Elements
Elements = Expr

UnaryOp = [not]
Eq = [equals]
Neq = [notequals]
Lt = [less]
Gt = [greater]
Add = [add]
Sub = [subtract]
Mul = [multiply]
Div = [divide]
Mod = [modulo]

Float = [[0-9]+\.[0-9]+]
Int = [[0-9]+]
String = ["[^"]*"]
Identifier = [[a-zA-Z_][a-zA-Z0-9_]*]
